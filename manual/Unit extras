[[tags: manual]]
[[toc:]]

== Unit extras

This unit contains a collection of useful utility definitions.
This unit is used by default, unless the program
is compiled with the {{-explicit-use}} option.



=== Lists


==== alist-ref

 [procedure] (alist-ref KEY ALIST [TEST [DEFAULT]])

Looks up {{KEY}} in {{ALIST}} using {{TEST}} as the comparison function (or {{eqv?}} if
no test was given) and returns the cdr of the found pair, or {{DEFAULT}} (which defaults to {{#f}}).


==== alist-update!

 [procedure] (alist-update! KEY VALUE ALIST [TEST])

If the list {{ALIST}} contains a pair of the form {{(KEY . X)}}, then this procedure
replaces {{X}} with {{VALUE}} and returns {{ALIST}}. If {{ALIST}} contains no such item, then
{{alist-update!}} returns {{((KEY . VALUE) . ALIST)}}. The optional argument
{{TEST}} specifies the comparison procedure to search a matching pair in {{ALIST}}
and defaults to {{eqv?}}.


==== atom?

 [procedure] (atom? X)

Returns {{#t}} if {{X}} is not a pair. This is identical to {{not-pair?}} from [[Unit srfi-1]] but
kept for historical reasons.


==== rassoc

 [procedure] (rassoc KEY LIST [TEST])

Similar to {{assoc}}, but compares {{KEY}} with the {{cdr}} of each pair in {{LIST}} using
{{TEST}} as the comparison procedures (which defaults to {{eqv?}}.


==== butlast

 [procedure] (butlast LIST)

Returns a fresh list with all elements but the last of {{LIST}}.


==== chop

 [procedure] (chop LIST N)

Returns a new list of sublists, where each sublist contains {{N}}
elements of {{LIST}}. If {{LIST}} has a length that is not
a multiple of {{N}}, then the last sublist contains the remaining
elements.

<enscript highlight=scheme>
(chop '(1 2 3 4 5 6) 2) ==> ((1 2) (3 4) (5 6))
(chop '(a b c d) 3)     ==> ((a b c) (d))
</enscript>


==== compress

 [procedure] (compress BLIST LIST)

Returns a new list with elements taken from {{LIST}} with
corresponding true values in the list {{BLIST}}.

<enscript highlight=scheme>
(define nums '(99 100 110 401 1234))
(compress (map odd? nums) nums)      ==> (99 401)
</enscript>


==== flatten

 [procedure] (flatten LIST1 ...)

Returns {{LIST1 ...}} concatenated together, with nested lists
removed (flattened).


==== intersperse

 [procedure] (intersperse LIST X)

Returns a new list with {{X}} placed between each element.


==== join

 [procedure] (join LISTOFLISTS [LIST])

Concatenates the lists in {{LISTOFLISTS}} with {{LIST}} placed
between each sublist. {{LIST}} defaults to the empty list.

<enscript highlight=scheme>
(join '((a b) (c d) (e)) '(x y)) ==> (a b x y c d x y e)
(join '((p q) () (r (s) t)) '(-))  ==> (p q - - r (s) t)
</enscript>

{{join}} could be implemented as follows:

<enscript highlight=scheme>
(define (join lstoflsts #!optional (lst '()))
  (apply append (intersperse lstoflists lst)) )
</enscript>


==== shuffle

 [procedure] (shuffle LIST)

Returns {{LIST}} with its elements sorted in a random order.


==== tail?

 [procedure] (tail? X LIST)

Returns true if {{X}} is one of the tails (cdr's) of {{LIST}}.



=== String-port extensions


==== call-with-input-string

 [procedure] (call-with-input-string STRING PROC)

Calls the procedure {{PROC}} with a single argument that is a
string-input-port with the contents of {{STRING}}.


==== call-with-output-string

 [procedure] (call-with-output-string PROC)

Calls the procedure {{PROC}} with a single argument that is a
string-output-port.  Returns the accumulated output-string.


==== with-input-from-string

 [procedure] (with-input-from-string STRING THUNK)

Call procedure {{THUNK}} with the current input-port temporarily
bound to an input-string-port with the contents of {{STRING}}.


==== with-output-to-string

 [procedure] (with-output-to-string THUNK)

Call procedure {{THUNK}} with the current output-port temporarily
bound to a string-output-port and return the accumulated output string.



=== Formatted output


==== printf
==== fprintf
==== sprintf

 [procedure] (fprintf PORT FORMATSTRING ARG ...)
 [procedure] (printf FORMATSTRING ARG ...)
 [procedure] (sprintf FORMATSTRING ARG ...)

Simple formatted output to a given port ({{fprintf}}), the
value of {{(current-output-port)}} ({{printf}}), or a string
({{sprintf}}).  The {{FORMATSTRING}} can contain any sequence
of characters.  There must be at least as many {{ARG}} arguments given as there are format directives that require an argument in {{FORMATSTRING}}.  Extra {{ARG}} arguments are ignored.  The character `~' prefixes special formatting directives:

<table>
<tr><td> ~%
write newline character
</td></tr><tr><td> ~N
the same as {{~%}}
</td></tr><tr><td> ~S
write the next argument
</td></tr><tr><td> ~A
display the next argument
</td></tr><tr><td> ~\n
skip all whitespace in the format-string until the next non-whitespace character
</td></tr><tr><td> ~B
write the next argument as a binary number
</td></tr><tr><td> ~O
write the next argument as an octal number
</td></tr><tr><td> ~X
write the next argument as a hexadecimal number
</td></tr><tr><td> ~C
write the next argument as a character
</td></tr><tr><td> ~~
display `~'
</td></tr><tr><td> ~!
flush all pending output
</td></tr><tr><td> ~?
invoke formatted output routine recursively with the next two arguments as format-string and list of parameters
</td></tr></table>


==== format

 [procedure] (format [DESTINATION] FORMATSTRING ARG ...)

The parameters  {{FORMATSTRING}} and {{ARG ...}} are as for
({{printf}}/{{sprintf}}/{{fprintf}}).

The optional {{DESTINATION}}, when supplied, performs
a ({{sprintf}}) for a  {{#f}},
a ({{printf}}) for a {{#t}},
and a ({{fprintf}}) for an output-port.
When missing a ({{sprintf}}) is performed.



=== Hash tables

CHICKEN implements SRFI 69 with SRFI 90 extensions. For more information, see
[[http://srfi.schemers.org/srfi-69/srfi-69.html|SRFI-69]] and
[[http://srfi.schemers.org/srfi-90/srfi-90.html|SRFI-90]].


==== make-hash-table

 [procedure] (make-hash-table [TEST HASH SIZE] #:TEST #:HASH #:SIZE #:INITIAL #:MIN-LOAD #:MAX-LOAD #:WEAK-KEYS #:WEAK-VALUES)

Returns a new {{HASH-TABLE}} with the supplied configuration.

; {{TEST}} : The equivalence function.
; {{HASH}} : The hash function.
; {{SIZE}} : The expected number of table elements.
; {{INITIAL}} : The default initial value.
; {{MIN-LOAD}} : The minimum load factor. A {{flonum}} in (0.0 1.0).
; {{MAX-LOAD}} : The maximum load factor. A {{flonum}} in (0.0 1.0).
; {{WEAK-KEYS}} : Use weak references for keys. (Ignored)
; {{WEAK-VALUES}} : Use weak references for values. (Ignored)

(No, the keyword parameters are not uppercase.)


==== hash-table?

 [procedure] (hash-table? OBJECT)

Is the {{OBJECT}} a {{hash-table}}?


==== hash-table-size

 [procedure] (hash-table-size HASH-TABLE)

The {{HASH-TABLE}} size.


==== hash-table-equivalence-function

 [procedure] (hash-table-equivalence-function HASH-TABLE)

The {{HASH-TABLE}} {{equivalence-function}}.


==== hash-table-hash-function

 [procedure] (hash-table-hash-function HASH-TABLE)

The {{HASH-TABLE}} {{hash-function}}.


==== hash-table-min-load

 [procedure] (hash-table-min-load HASH-TABLE)

The {{HASH-TABLE}} minimum load factor.


==== hash-table-max-load

 [procedure] (hash-table-max-load HASH-TABLE)

The {{HASH-TABLE}} maximum load factor.


==== hash-table-weak-keys

 [procedure] (hash-table-weak-keys HASH-TABLE)

Does the {{HASH-TABLE}} weak references for keys?


==== hash-table-weak-values

 [procedure] (hash-table-weak-values HASH-TABLE)

Does the {{HASH-TABLE}} weak references for values?


==== hash-table-has-initial?

 [procedure] (hash-table-has-initial? HASH-TABLE)

Does the {{HASH-TABLE}} have a default initial value?


==== hash-table-initial

 [procedure] (hash-table-initial HASH-TABLE)

The {{HASH-TABLE}} default initial value.


==== hash-table-keys

 [procedure] (hash-table-keys HASH-TABLE)

Returns a list of the keys in the {{HASH-TABLE}} population.


==== hash-table-values

 [procedure] (hash-table-values HASH-TABLE)

Returns a list of the values in the {{HASH-TABLE}} population.


==== hash-table->alist

 [procedure] (hash-table->alist HASH-TABLE)

Returns the population of the {{HASH-TABLE}} as an {{association-list}}.


==== alist->hash-table

 [procedure] (alist->hash-table ASSOCIATION-LIST [MAKE-HASH-TABLE-PARAMETER ...])

Returns a new {{HASH-TABLE}}, configured using the optional
{{MAKE-HASH-TABLE-PARAMETER ...}}. The {{HASH-TABLE}} is populated from the
{{ASSOCIATION-LIST}}.


==== hash-table-ref

 [procedure] (hash-table-ref HASH-TABLE KEY)

Returns the {{VALUE}} for the {{KEY}} in the {{HASH-TABLE}}.

Aborts with an exception when the {{KEY}} is missing.


==== hash-table-ref/default

 [procedure] (hash-table-ref/default HASH-TABLE KEY DEFAULT)

Returns the {{VALUE}} for the {{KEY}} in the {{HASH-TABLE}}, or the {{DEFAULT}}
when the {{KEY}} is missing.


==== hash-table-exists?

 [procedure] (hash-table-exists? HASH-TABLE KEY)

Does the {{KEY}} exist in the {{HASH-TABLE}}?


==== hash-table-set!

 [procedure] (hash-table-set! HASH-TABLE KEY VALUE)

Set the {{VALUE}} for the {{KEY}} in the {{HASH-TABLE}}.

A setter for {{hash-table-ref}} is defined, so

<enscript highlight=scheme>
(set! (hash-table-ref HASH-TABLE KEY) VALUE)
</enscript>

is equivalent to

<enscript highlight=scheme>
(hash-table-set! HASH-TABLE KEY VALUE)
</enscript>


==== hash-table-update!

 [procedure] (hash-table-update! HASH-TABLE KEY [UPDATE-FUNCTION [DEFAULT-VALUE-FUNCTION]])

Sets or replaces the {{VALUE}} for {{KEY}} in the {{HASH-TABLE}}.

The {{UPDATE-FUNCTION}} takes the existing {{VALUE}} for {{KEY}} and returns
the new {{VALUE}}. The default is {{identity}}

The {{DEFAULT-VALUE-FUNCTION}} is called when the entry for {{KEY}} is missing.
The default uses the {{(hash-table-initial-value)}}, if provided. Otherwise
aborts with an exception.

Returns the new {{VALUE}}.


==== hash-table-update!/default

 [procedure] (hash-table-update! HASH-TABLE KEY UPDATE-FUNCTION DEFAULT-VALUE)

Sets or replaces the {{VALUE}} for {{KEY}} in the {{HASH-TABLE}}.

The {{UPDATE-FUNCTION}} takes the existing {{VALUE}} for {{KEY}} and returns
the new {{VALUE}}.

The {{DEFAULT-VALUE}} is used when the entry for {{KEY}} is missing.

Returns the new {{VALUE}}.


==== hash-table-copy

 [procededure] (hash-table-copy HASH-TABLE)

Returns a shallow copy of the {{HASH-TABLE}}.


==== hash-table-delete!

 [procedure] (hash-table-delete! HASH-TABLE KEY)

Deletes the entry for {{KEY}} in the {{HASH-TABLE}}.


==== hash-table-remove!

 [procedure] (hash-table-remove! HASH-TABLE PROC)

Calls {{PROC}} for all entries in {{HASH-TABLE}} with the key and value of each
entry. If {{PROC}} returns true, then that entry is removed.


==== hash-table-merge

 [procedure] (hash-table-merge HASH-TABLE-1 HASH-TABLE-2)

Returns a new {{HASH-TABLE}} with the union of {{HASH-TABLE-1}} and
{{HASH-TABLE-2}}.


==== hash-table-merge!

 [procedure] (hash-table-merge! HASH-TABLE-1 HASH-TABLE-2)

Returns {{HASH-TABLE-1}} as the union of {{HASH-TABLE-1}} and
{{HASH-TABLE-2}}.


==== hash-table-map

 [procedure] (hash-table-map HASH-TABLE FUNC)

Calls {{FUNC}} for all entries in {{HASH-TABLE}} with the key and value of each
entry.

Returns a list of the results of each call.


==== hash-table-fold

 [procedure] (hash-table-fold HASH-TABLE FUNC INIT)

Calls {{FUNC}} for all entries in {{HASH-TABLE}} with the key and value of each
entry, and the current folded value. The initial folded value is {{INIT}}.

Returns the final folded value.


==== hash-table-for-each

 [procedure] (hash-table-for-each HASH-TABLE PROC)

Calls {{PROC}} for all entries in {{HASH-TABLE}} with the key and value of each
entry.


==== hash-table-walk

 [procedure] (hash-table-walk HASH-TABLE PROC)

Calls {{PROC}} for all entries in {{HASH-TABLE}} with the key and value of each
entry.


=== Hash Functions

All hash functions return a {{fixnum}} in the range [0 {{BOUND}}).


==== number-hash

 [procedure] (number-hash NUMBER [BOUND])

For use with {{=}} as a {{hash-table-equivalence-function}}.


==== object-uid-hash

 [procedure] (object-uid-hash OBJECT [BOUND])

Currently a synonym for {{equal?-hash}}.


==== symbol-hash

 [procedure] (symbol-hash SYMBOL [BOUND])

For use with {{eq?}} as a {{hash-table-equivalence-function}}.


==== keyword-hash

 [procedure] (keyword-hash KEYWORD [BOUND])

For use with {{eq?}} as a {{hash-table-equivalence-function}}.


==== string-hash

 [procedure] (string-hash STRING [BOUND])

For use with {{string=?}} as a {{hash-table-equivalence-function}}.


==== string-ci-hash

 [procedure] (string-ci-hash STRING [BOUND])

For use with {{string-ci=?}} as a {{hash-table-equivalence-function}}.


==== eq?-hash

 [procedure] (eq?-hash OBJECT [BOUND])

For use with {{eq?}} as a {{hash-table-equivalence-function}}.


==== eqv?-hash

 [procedure] (eqv?-hash OBJECT [BOUND])

For use with {{eqv?}} as a {{hash-table-equivalence-function}}.


==== equal?-hash

 [procedure] (equal?-hash OBJECT [BOUND])

For use with {{equal?}} as a {{hash-table-equivalence-function}}.


==== hash

 [procedure] (hash OBJECT [BOUND])

Synonym for {{equal?-hash}}.


==== hash-by-identity

 [procedure] (hash-by-identity OBJECT [BOUND])

Synonym for {{eq?-hash}}.



=== Queues


==== list->queue

 [procedure] (list->queue LIST)

Returns {{LIST}} converted into a queue, where the first element
of the list is the same as the first element of the queue. The resulting
queue may share memory with the list and the list should not be modified
after this operation.


==== make-queue

 [procedure] (make-queue)

Returns a newly created queue.


==== queue?

 [procedure] (queue? X)

Returns {{#t}} if {{X}} is a queue, or {{#f}} otherwise.


==== queue->list

 [procedure] (queue->list QUEUE)

Returns {{QUEUE}} converted into a list, where the first element
of the list is the same as the first element of the queue. The resulting
list may share memory with the queue object and should not be modified.


==== queue-add!

 [procedure] (queue-add! QUEUE X)

Adds {{X}} to the rear of {{QUEUE}}.


==== queue-empty?

 [procedure] (queue-empty? QUEUE)

Returns {{#t}} if {{QUEUE}} is empty, or {{#f}} otherwise.


==== queue-first

 [procedure] (queue-first QUEUE)

Returns the first element of {{QUEUE}}. If {{QUEUE}} is empty
an error is signaled


==== queue-last

 [procedure] (queue-last QUEUE)

Returns the last element of {{QUEUE}}. If {{QUEUE}} is empty
an error is signaled


==== queue-remove!

 [procedure] (queue-remove! QUEUE)

Removes and returns the first element of {{QUEUE}}. If {{QUEUE}}
is empty an error is signaled


==== queue-push-back!

 [procedure] (queue-push-back! QUEUE ITEM)

Pushes an item into the first position of a queue, i.e. the next
{{queue-remove!}} will return {{ITEM}}.


==== queue-push-back-list!

 [procedure] (queue-push-back-list! QUEUE LIST)

Pushes the items in item-list back onto the queue,
so that {{(car LIST)}} becomes the next removable item.



=== Sorting


==== merge

 [procedure] (merge LIST1 LIST2 LESS?)
 [procedure] (merge! LIST1 LIST2 LESS?)

Joins two lists in sorted order. {{merge!}} is the destructive
version of merge. {{LESS?  }} should be a procedure of two arguments,
that returns true if the first argument is to be ordered before the
second argument.


==== sort

 [procedure] (sort SEQUENCE LESS?)
 [procedure] (sort! SEQUENCE LESS?)

Sort {{SEQUENCE}}, which should be a list or a vector. {{sort!}}
is the destructive version of sort.


==== sorted?

 [procedure] (sorted? SEQUENCE LESS?)

Returns true if the list or vector {{SEQUENCE}} is already sorted.



=== Random numbers


==== random-seed

 [procedure] (random-seed [SEED])

Seeds the random number generator with {{SEED}} (an exact integer) or
{{(current-seconds)}} if {{SEED}} is not given.


==== random

 [procedure] (random N)

Returns an exact random integer from 0 to {{N}}-1.


==== randomize

 [procedure] (randomize [X])

Set random-number seed. If {{X}} is not supplied, the current time is used.
On startup (when the {{extras}} unit is initialized), the random number generator
is initialized with the current time.



=== Input/Output extensions


==== make-input-port

 [procedure] (make-input-port READ READY? CLOSE [PEEK])

Returns a custom input port. Common operations on this
port are handled by the given parameters, which should be
procedures of no arguments. {{READ}} is called when the
next character is to be read and should return a character or
{{#!eof}}. {{READY?}} is called
when {{char-ready?}} is called on this port and should return
{{#t}} or {{#f}}.  {{CLOSE}} is called when the port is
closed. {{PEEK}} is called when {{peek-char}} is called on this
port and should return a character or {{#!eof}}.
if the argument {{PEEK}} is not given, then {{READ}} is used
instead and the created port object handles peeking automatically (by
calling {{READ}} and buffering the character).


==== make-output-port

 [procedure] (make-output-port WRITE CLOSE [FLUSH])

Returns a custom output port. Common operations on this port are handled
by the given parameters, which should be procedures.  {{WRITE}} is
called when output is sent to the port and receives a single argument,
a string.  {{CLOSE}} is called when the port is closed and should
be a procedure of no arguments. {{FLUSH}} (if provided) is called
for flushing the output port.


==== pretty-print

 [procedure] (pretty-print EXP [PORT])
 [procedure] (pp EXP [PORT])

Print expression nicely formatted. {{PORT}} defaults to the value
of {{(current-output-port)}}.


==== pretty-print-width
(Parameter) Specifies the maximal line-width for pretty printing, after which line
wrap will occur.

==== read-byte
==== write-byte

 [procedure] (read-byte [PORT])
 [procedure] (write-byte BYTE [PORT])

Read/write a byte to the port given in {{PORT}}, which default to the values
of {{(current-input-port)}} and {{(current-output-port)}}, respectively.

==== read-file

 [procedure] (read-file [FILE-OR-PORT [READER [MAXCOUNT]]])

Returns a list containing all toplevel expressions
read from the file or port {{FILE-OR-PORT}}. If no argument is given,
input is read from the port that is the current value of {{(current-input-port)}}.
After all expressions are read, and if the argument is a port, then the port will
not be closed. The {{READER}} argument specifies the procedure used to read
expressions from the given file or port and defaults to {{read}}. The reader
procedure will be called with a single argument (an input port).
If {{MAXCOUNT}} is given then only up to {{MAXCOUNT}} expressions will be read in.


==== read-line
==== write-line

 [procedure] (read-line [PORT [LIMIT]])
 [procedure] (write-line STRING [PORT])

Line-input and -output. {{PORT}} defaults to the value of
{{(current-input-port)}} and {{(current-output-port)}},
respectively. If the optional argument {{LIMIT}} is given and
not {{#f}}, then {{read-line}} reads at most {{LIMIT}}
characters per line. {{read-line}} returns a string without the terminating newline and {{write-line}} adds a terminating newline  before outputting.


==== read-lines

 [procedure] (read-lines [PORT [MAX]])

Read {{MAX}} or fewer lines from {{PORT}}. {{PORT}}
defaults to the value of {{(current-input-port)}}. {{PORT}} may optionally be
a string naming a file. Returns a list of strings, each string representing a line read, not including any line separation character(s).


==== read-string
==== read-string!
==== write-string

 [procedure] (read-string [NUM [PORT]])
 [procedure] (read-string! NUM STRING [PORT [START]])
 [procedure] (write-string STRING [NUM [PORT]]

Read or write {{NUM}} characters from/to {{PORT}}, which defaults to the
value of {{(current-input-port)}} or {{(current-output-port)}}, respectively.
If {{NUM}} is {{#f}} or not given, then all data
up to the end-of-file is read, or, in the case of {{write-string}} the whole
string is written. If no more input is available, {{read-string}} returns the
empty string. {{read-string!}} reads destructively into the given {{STRING}} argument,
but never more characters that would fit into {{STRING}}. If {{START}} is given, then
the read characters are stored starting at that position.
{{read-string!}} returns the actual number of characters read.


==== read-token

 [procedure] (read-token PREDICATE [PORT])

Reads characters from {{PORT}} (which defaults to the value of {{(current-input-port)}})
and calls the procedure {{PREDICATE}} with each character until {{PREDICATE}} returns
false. Returns a string with the accumulated characters.


==== with-error-output-to-port

 [procedure] (with-error-output-to-port PORT THUNK)

Call procedure {{THUNK}} with the current error output-port
temporarily bound to {{PORT}}.


==== with-input-from-port

 [procedure] (with-input-from-port PORT THUNK)

Call procedure {{THUNK}} with the current input-port temporarily
bound to {{PORT}}.


==== with-output-to-port

 [procedure] (with-output-to-port PORT THUNK)

Call procedure {{THUNK}} with the current output-port temporarily
bound to {{PORT}}.



=== Strings


==== conc

 [procedure] (conc X ...)

Returns a string with the string-represenation of all arguments concatenated
together. {{conc}} could be implemented as

<enscript highlight=scheme>
(define (conc . args)
  (apply string-append (map ->string args)) )
</enscript>



==== ->string

 [procedure] (->string X)

Returns a string-representation of {{X}}.


==== string-chop

 [procedure] (string-chop STRING LENGTH)

Returns a list of substrings taken by ''chopping'' {{STRING}} every {{LENGTH}}
characters:

<enscript highlight=scheme>
(string-chop "one two three" 4)  ==>  ("one " "two " "thre" "e")
</enscript>



==== string-chomp

 [procedure] (string-chomp STRING [SUFFIX])

If {{STRING}} ends with {{SUFFIX}}, then this procedure returns a copy of its first argument with the suffix
removed, otherwise returns {{STRING}} unchanged. {{SUFFIX}} defaults to {{"\n"}}.


==== string-compare3

 [procedure] (string-compare3 STRING1 STRING2)
 [procedure] (string-compare3-ci STRING1 STRING2)

Perform a three-way comparison between the {{STRING1}} and {{STRING2}},
returning either {{-1}} if {{STRING1}} is lexicographically less
than {{STRING2}}, {{0}} if it is equal, or {{1}} if it s greater.
{{string-compare3-ci}} performs a case-insensitive comparison.


==== string-intersperse

 [procedure] (string-intersperse LIST [STRING])

Returns a string that contains all strings in {{LIST}} concatenated
together.  {{STRING}} is placed between each concatenated string and
defaults to {{" "}}.

<enscript highlight=scheme>
(string-intersperse '("one" "two") "three")
</enscript>

is equivalent to

<enscript highlight=scheme>
(apply string-append (intersperse '("one" "two") "three"))
</enscript>


==== string-split

 [procedure] (string-split STRING [DELIMITER-STRING [KEEPEMPTY]])

Split string into substrings separated by the given delimiters. If
no delimiters are specified, a string comprising the tab, newline and space characters
is assumed. If the
parameter {{KEEPEMPTY}} is given and not {{#f}}, then empty
substrings are retained:

<enscript highlight=scheme>
(string-split "one  two  three") ==> ("one" "two" "three")
(string-split "foo:bar::baz:" ":" #t) ==> ("foo" "bar" "" "baz" "")
</enscript>


==== string-translate

 [procedure] (string-translate STRING FROM [TO])

Returns a fresh copy of {{STRING}} with characters matching
{{FROM}} translated to {{TO}}.  If {{TO}} is omitted, then
matching characters are removed. {{FROM}} and {{TO}} may be
a character, a string or a list. If both {{FROM}} and {{TO}}
are strings, then the character at the same position in {{TO}}
as the matching character in {{FROM}} is substituted.


==== string-translate*

 [procedure] (string-translate* STRING SMAP)

Substitutes elements of {{STRING}} according to {{SMAP}}.
{{SMAP}} should be an association-list where each element of the list
is a pair of the form {{(MATCH \. REPLACEMENT)}}. Every occurrence of
the string {{MATCH}} in {{STRING}} will be replaced by the string
{{REPLACEMENT}}:

<enscript highlight=scheme>
(string-translate*
  "<h1>this is a \"string\"</h1>"
  '(("<" . "&lt;") (">" . "&gt;") ("\"" . "&quot;")) )
=>  "&lt;h1&gt;this is a &quot;string&quot;&lt;/h1&gt;"
</enscript>


==== substring=?

 [procedure] (substring=? STRING1 STRING2 [START1 [START2 [LENGTH]]])
 [procedure] (substring-ci=? STRING1 STRING2 [START1 [START2 [LENGTH]]])

Returns {{#t}} if the strings {{STRING1}} and {{STRING2}} are equal, or
{{#f}} otherwise.
The comparison starts at the positions {{START1}} and {{START2}} (which default
to 0), comparing {{LENGTH}} characters (which defaults to the minimum of the remaining
length of both strings).


==== substring-index

 [procedure] (substring-index WHICH WHERE [START])
 [procedure] (substring-index-ci WHICH WHERE [START])

Searches for first index in string {{WHERE}} where string
{{WHICH}} occurs.  If the optional argument {{START}} is given,
then the search starts at that index.  {{substring-index-ci}}
is a case-insensitive version of {{substring-index}}.



=== Combinators


==== any?

 [procedure] (any? X)

Ignores its argument and always returns {{#t}}. This is actually useful sometimes.


==== none?

 [procedure] (none? X)

Ignores its argument and always returns {{#f}}. This is actually useful sometimes.


==== always?

 [procedure] (always? ...)

Ignores its arguments and always returns {{#t}}. This is actually useful sometimes.


==== never?

 [procedure] (never? ...)

Ignores its arguments and always returns {{#f}}. This is actually useful sometimes.


==== constantly

 [procedure] (constantly X ...)

Returns a procedure that always returns the values {{X ...}} regardless of the number and value of its arguments.

<enscript highlight=scheme>
(constantly X) <=> (lambda args X)
</enscript>


==== complement

 [procedure] (complement PROC)

Returns a procedure that returns the boolean inverse of {{PROC}}.

<enscript highlight=scheme>
(complement PROC) <=> (lambda (x) (not (PROC x)))
</enscript>


==== compose

 [procedure] (compose PROC1 PROC2 ...)

Returns a procedure that represents the composition of the
argument-procedures {{PROC1 PROC2 ...}}.

<enscript highlight=scheme>
(compose F G) <=> (lambda args
                      (call-with-values
                         (lambda () (apply G args))
                         F))
</enscript>

{{(compose)}} is equivalent to {{values}}.


==== conjoin

 [procedure] (conjoin PRED ...)

Returns a procedure that returns {{#t}} if its argument satisfies the
predicates {{PRED ...}}.
<enscript highlight=scheme>
((conjoin odd? positive?) 33)   ==>  #t
((conjoin odd? positive?) -33)  ==>  #f
</enscript>


==== disjoin

 [procedure] (disjoin PRED ...)

Returns a procedure that returns {{#t}} if its argument satisfies any
predicate {{PRED ...}}.
<enscript highlight=scheme>
((disjoin odd? positive?) 32)    ==>  #t
((disjoin odd? positive?) -32)   ==>  #f
</enscript>


==== each

 [procedure] (each PROC ...)

Returns a procedure that applies {{PROC ...}} to its arguments, and returns the result(s)
of the last procedure application. For example

<enscript highlight=scheme>
(each pp eval)
</enscript>

is equivalent to

<enscript highlight=scheme>
(lambda args
  (apply pp args)
  (apply eval args) )
</enscript>

{{(each PROC)}} is equivalent to {{PROC}} and {{(each)}} is equivalent to
{{noop}}.


==== flip

 [procedure] (flip PROC)

Returns a two-argument procedure that calls {{PROC}} with its
arguments swapped:
<enscript highlight=scheme>
(flip PROC) <=> (lambda (x y) (PROC y x))
</enscript>


==== identity

 [procedure] (identity X)

Returns its sole argument {{X}}.


==== project

 [procedure] (project N)

Returns a procedure that returns its {{N}}th argument (starting from 0).


==== list-of

 [procedure] (list-of PRED)

Returns a procedure of one argument that returns {{#t}} when
applied to a list of elements that all satisfy the predicate procedure
{{PRED}}, or {{#f}} otherwise.

<enscript highlight=scheme>
((list-of even?) '(1 2 3))   ==> #f
((list-of number?) '(1 2 3)) ==> #t
</enscript>


==== noop

 [procedure] (noop X ...)

Ignores it's arguments, does nothing and returns an unspecified value.


==== o

 [procedure] (o PROC ...)

A single value version of {{compose}} (slightly faster). {{(o)}} is equivalent
to {{identity}}.


==== left-section

 [procedure] (left-section PROC ARG0 ...)

Returns an n-ary procedure that partially applies its' arguments {{ARG0 ...}}
from the left (normal order). Sort of a n-ary curry.


==== right-section

 [procedure] (right-section PROC ARG0 ...)

Returns an n-ary procedure that partially applies its' arguments {{ARG0 ...}}
from the right (reverse order). Sort of a n-ary curry.


=== Binary searching


==== binary-search

 [procedure] (binary-search SEQUENCE PROC)

Performs a binary search in {{SEQUENCE}}, which should be a sorted
list or vector.  {{PROC}} is called to compare items in the sequence,
should accept a single argument and return an exact integer: zero if the
searched value is equal to the current item, negative if the searched
value is ''less'' than the current item, and positive otherwise.
Returns the index of the found value or {{#f}} otherwise.

Previous: [[Unit eval]]

Next: [[Unit srfi-1]]
